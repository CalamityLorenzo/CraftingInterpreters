program         -> declaration* EOF;
declaration     -> classDecl 
                | funcDecl 
                | varDecl 
                | statement;
classDecl       -> "class" IDENTIFIER "{" function* "}";                    
funcDecl        -> "fun" function | "class" function;
function        -> IDENTIFIER "(" parameters? ")" block;
parameters      -> IDENTIFIER ( "," IDENTIFIER )*;
varDecl         -> "var" IDENTIFIER ("=" expression)? ";" ;
statement       -> exprStmt 
                | forStmt
                | ifStmt
                | printStmt
                | returnStmt
                | whileStmt
                | block;                
exprStmt        -> expression ";" ;
forStmt         -> "for" "(" (varDecl| exprStmt | ";") 
                   expression? ";" 
                   expression? ")" statement;       
ifStmt          -> "if" "(" expression ")" statement
                   ( "else" statement)? ;
whileStmt       -> "while" "(" expression ")" statement ;

block           -> "{"  declaration* "}";
printStmt       -> "print" expression ";" ;
expression      -> assignment;
assignment      -> (call "." )? IDENTIFIER "=" assignment | logic_or; // Call syntax is for: obj.IDENTIFIER =  <<Assignment>> (recurse down the hill of getters to our top setter.)
logic_or        -> logic_and ( "or"  logic_and)*;
logic_and       -> equality ( "and" equality )*;
equality        -> comparison (("!=" | "==") comparison )*;
comparison      -> term (("<" | ">" | "<=" | ">=") term)*; 
term            -> factor (("+" | "-") factor)*;
factor          -> unary (("*" | "\") unary)*;
unary           -> ("-" | "!" ) unary | call;
call            -> primary ( "(" arguments? ")" | "." IDENTIFIER )*;   // * allows series of calls MyFunc(1)(2)() etc. | Class Objects Obj.Notation
arguments       -> expression ("," expression )*;       // Arguments is handled as if was an object {arg:one, arg2:two}
primary         -> NUMBER 
                | STRING 
                | "true" 
                | "false" 
                | "nil" 
                | "(" expression ")"
                | IDENTIFIER; 

Notes
======
Post fix grammer operators
* 0:N
+ 1:N
? 0:1 //Optional.